plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.github.node-gradle.node" version "3.1.1"
}

node {
	version = '14.17.0'
	npmVersion = '6.14.13'
	workDir = file("./front-end")
	npmWorkDir = file("./front-end")
	nodeModulesDir = file("./front-end")
}

group = 'com.taskagile'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'mysql:mysql-connector-java'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'junit:junit:4.12'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
}

task deleteVueArtifact(type: Delete) {
	description = "Delete prev build"
	delete "src/main/resources/static"
}

task setUp(type: NpmTask) {
	description = "Install Node.js packges"
	args = ['install']
	inputs.files file('package.json')
	outputs.files file('node_modules')
}

task buildFrontEnd(type: NpmTask, dependsOn: ['deleteVueArtifact', 'setUp']) {
	description = "Build vue.js"
	args = ['run', 'build']
}

processResources.dependsOn 'buildFrontEnd'

test {
	useJUnitPlatform()
}
